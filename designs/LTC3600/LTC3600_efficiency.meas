* -----------------------------------------------------------------
* LTC3600_efficiency.meas
*
* 9/20/2012 D. W. Hawkins (dwh@ovro.caltech.edu)
*
* LTC3600 power supply efficiency measurement.
*
* The power calculation equations were generated using
* LTspice's ALT-click feature, which adds component power
* to the LTspice plot window. The equations were copied
* into the .measure AVG statements below.
*
* The P_in, P_out, and eff calculations are all that is required
* to calculate efficiency. The additional component power
* measurements are included so that the relative power dissipation
* of components can be analyzed and optimized.
*
* The efficiency and power calculations match those generated by
* the built-in LTspice efficiency calculator.
*
* This script can be .included into the schematic, and the results
* will be output to the .log file. Alternatively, this script can
* be run after the simulation has been completed by selecting the
* plot window, selecting File->Execute .MEAS Script, and running
* this script. The results are output to an .mout file.
*
* -----------------------------------------------------------------
.measure tran P_in    AVG -V(IN)*I(Vin)
.measure tran P_out   AVG  V(OUT)*I(Rload)
.measure tran eff param P_out/P_in*100
.measure tran P_L     AVG  V(SW,OUT)*I(L)
.measure tran P_Cout  AVG  V(OUT)*I(Cout)
.measure tran P_U1    AVG  V(VSET)*Ix(U1:Iset)+V(VCC)*Ix(U1:Ith)+V(VCC)*Ix(U1:Rt)+V(VCC)*Ix(U1:PGFB)+V(IN)*Ix(U1:Run)+V(VCC)*Ix(U1:Mode/SYNC)+V(SW)*Ix(U1:SW)+V(IN)*Ix(U1:Vin)+V(BOOST)*Ix(U1:Boost)+V(VCC)*Ix(U1:INTVcc)+V(OUT)*Ix(U1:Vout)



